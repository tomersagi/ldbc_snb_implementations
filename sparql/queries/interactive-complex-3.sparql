PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>

SELECT (?friend as ?personId) (?first as ?personFirstName) (?last as ?personLastName) ?xCount ?yCount ((?xCount + ?yCount) as ?count)
WHERE {
  ?friend snvoc:firstName    ?first;
          snvoc:lastName     ?last.
  {
    SELECT ?friend (COUNT(?xMessage) as ?xCount )
    WHERE {
      ?friend   snvoc:isLocatedIn  ?city.
      ?xMessage snvoc:hasCreator   ?friend;
                snvoc:isLocatedIn  $Country1;
                snvoc:creationDate ?date.
      FILTER (
        !exists { ?city snvoc:isPartOf $Country1} &&
        ?date >= $Date0 && ?date < ($Date0 + $Duration) )
      {
        SELECT DISTINCT ?friend
        WHERE {
          {
            $Person snvoc:knows ?tfr.
            ?tfr    snvoc:hasPerson ?friend.
          } union {
            $Person snvoc:knows ?tfr2.
            ?tfr2   snvoc:hasPerson ?fr2.
            ?fr2    snvoc:knows ?tfr.
            ?tfr    snvoc:hasPerson ?friend.
            filter (?friend != $Person)
          } .
        }
      }

    }
    GROUP BY ?friend

  }
  {

    SELECT ?friend (COUNT(?yMessage) as ?yCount )
    WHERE {
      ?friend   snvoc:isLocatedIn  ?city.
      ?yMessage snvoc:hasCreator   ?friend;
                snvoc:isLocatedIn  $Country2;
                snvoc:creationDate ?date.
      FILTER (
        !exists { ?city snvoc:isPartOf $Country2} &&
        ?date >= $Date0 && ?date < ($Date0 + $Duration) )
      {
        SELECT DISTINCT ?friend
        WHERE {
          {
            $Person snvoc:knows ?tfr.
            ?tfr    snvoc:hasPerson ?friend.
          } union {
            $Person snvoc:knows ?tfr2.
            ?tfr2   snvoc:hasPerson ?fr2.
            ?fr2    snvoc:knows ?tfr.
            ?tfr    snvoc:hasPerson ?friend.
            filter (?friend != $Person)
          } .
        }
      }

    }
    GROUP BY ?friend

  }
  # FILTER (  ?xCount > 0 && ?yCount > 0)
}
ORDER BY DESC(?xCount) ASC(?friend)